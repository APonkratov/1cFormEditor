////////////////////////////////////////////////////////////////////////////////
// Модуль "ккПодготовкаФорм" содержит процедуры и функции для 
// подговтоки форм для работы с нетиповым фнукционалом системы.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет выполнялась ли уже подготовка формы для работы с нетиповым функционалом системы.
//
// Параметры:
// Форма - УправляемаяФорма
//
Функция ФормаПодготовлена(Форма) Экспорт
	
	Если Форма.Элементы.Найти("ккФормаПодготовлена") = Неопределено Тогда
		ФормаПодготовлена = Форма.Элементы.Добавить("ккФормаПодготовлена", Тип("ДекорацияФормы"));
		ФормаПодготовлена.Видимость = Ложь;
		ФормаПодготовлена.Доступность = Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция СоздатьКонтекстЭлемента(Форма, Родитель = Неопределено, РасположитьПередЭлементом = Неопределено) Экспорт
	
	СтруктураКонтекст = Новый Структура();
	СтруктураКонтекст.Вставить("Форма", Форма);
	СтруктураКонтекст.Вставить("Родитель", Родитель);
	СтруктураКонтекст.Вставить("РасположитьПередЭлементом", РасположитьПередЭлементом);
	
	Возврат СтруктураКонтекст;
	
КонецФункции


#Область ДобавлениеПолей

// Функция добавляет поле на форму
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяПоля - Строка - Имя создааваемого поля.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//	Родитель - Произвольный - Ссылка на родителя.
//	ЭтоПолеВвода - Булево - Определяет возможность ввода в добавляемое поле
//
// Возвращаемое значение:
//	ЭлементФормы - добавленный на форму, новый элемент формы.
//
//Пример:
//
// Добавление элеента табличной части
//	ПараметрыОтображения = Новый Структура;
//	ПараметрыОтображения.Вставить("ПутьКДанным", "Объект.РасшифровкаПлатежа.ккОснованиеПлатежа");
//	ПараметрыОтображения.Вставить("Вид", ВидПоляФормы.ПолеВвода);
//	ккПодготовкаФорм.ДобавитьПолеНаФорму(ЭтаФорма, "ккРасшифровкаПлатежаОснованиеПлатежа", ПараметрыОтображения, Элементы.РасшифровкаПлатежа);
//
// Добавление элмента на форму
//	ПараметрыОтображения.Вставить("ПутьКДанным", "ВидДоговора");
//	ккПодготовкаФорм.ДобавитьПолеНаФорму(ЭтаФорма, "ВидДоговора", ПараметрыОтображения, Элементы.БыстрыеОтборы, Истина);
//
Функция ДобавитьПолеНаФорму(КонтекстЭлемента,
							ИмяПоля,
							ПараметрыОтображения) Экспорт
							
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
	
	ПолеФормы = Форма.Элементы.Добавить(ИмяПоля, Тип("ПолеФормы"), Родитель);

	ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;

	ЗаполнитьЗначенияСвойств(ПолеФормы,ПараметрыОтображения);	
	РасположитьПередЭлементом(КонтекстЭлемента, ПолеФормы);
	
	Возврат ПолеФормы;
	
КонецФункции

// Функция добавляет поле на форму для реквизита формы
//
// Параметры:
//	Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяРеквизита - Строка - Имя реквизита формы.
//	Родитель - Произвольный - Ссылка на родителя.
//	ЭтоПолеВвода - Булево - Определяет возможность ввода в добавляемое поле
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//
//Пример:
//
//	ккПодготовкаФорм.ДобавитьПолеНаФормуРеквизитФормы(ЭтаФорма, "ккПравилаОплаты", Элементы.ГруппаСлева, Истина);
//
Функция ДобавитьПолеНаФормуРеквизитФормы(КонтекстЭлемента, 
											ИмяРеквизита,  
											ПараметрыОтображения = Неопределено) Экспорт
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
		
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		Если Не ПараметрыОтображения.Свойство("ПутьКДанным") Тогда
			ПараметрыОтображения.Вставить("ПутьКДанным", ИмяРеквизита);
		КонецЕсли;
	Иначе
		ПараметрыОтображения = Новый Структура;
		ПараметрыОтображения.Вставить("ПутьКДанным", ИмяРеквизита);
	КонецЕсли;
	
	ПолеФормы = Форма.Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Родитель);

	ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;

	ЗаполнитьЗначенияСвойств(ПолеФормы, ПараметрыОтображения);
	РасположитьПередЭлементом(КонтекстЭлемента, ПолеФормы);
		
	ПараметрыОтображения.Удалить("ПутьКДанным");
	
	Возврат ПолеФормы
	
КонецФункции

#КонецОбласти

#Область ДобавлениеГрупп

// Процедура добавляет группу на форму
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяГруппы - Строка - Имя создаваемой группы.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства.
//	Родитель - Произвольный - Ссылка на родителя.
//	РасположитьПередЭлементом - ВсеЭлементыФормы - Указывает, перед каким элеменом формы разместить добавляемую группу
//
// Возвращаемое значение:
//	ЭлементФормы - добавленный на форму, новый элемент формы.
//
//	ПараметрыОтображения = Новый Структура("Вид, Группировка, ОтображатьЗаголовок", ВидГруппыФормы.ОбычнаяГруппа, ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяЕслиВозможно, Ложь);
//	ЭлементГруппаБулево = ккПодготовкаФорм.ДобавитьГруппуНаФорму(ЭтаФорма, "ГруппаБулево", ПараметрыОтображения); 
//
Функция ДобавитьГруппуНаФорму(КонтекстЭлемента, 
								ИмяГруппы, 
								ПараметрыОтображения = Неопределено) Экспорт
	
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
	
	ГруппаФормы = Форма.Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), Родитель);
	
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ГруппаФормы,ПараметрыОтображения);
	КонецЕсли;
	
	РасположитьПередЭлементом(КонтекстЭлемента, ГруппаФормы);

	Возврат ГруппаФормы;
	
КонецФункции



// Процедура добавляет группу и 2 вложенные группы(ИмяОсновнойГруппы+"Лево" и ИмяОсновнойГруппы+"Право") на форму 
//	основная группа с горизонтальной группировкой, вложенные группы с вертикальной группировкой
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяГруппы - Строка - Имя создаваемой группы.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства.
//	Родитель - Произвольный - Ссылка на родителя.
//	РасположитьПередЭлементом - ВсеЭлементыФормы - Указывает, перед каким элеменом формы разместить добавляемую группу
//
// Возвращаемое значение:
//	ЭлементФормы - добавленный на форму, основная группа, которые содержит группы, которые являются колонками.
//
Функция ДобавитьГруппуСКолонкамиЛевоИПраво(КонтекстЭлемента, ИмяОсновнойГруппы, ПараметрыОтображения = Неопределено) Экспорт
	
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
		
	ИмяГруппыЛево = ИмяОсновнойГруппы+"Лево";
	ИмяГруппыПраво = ИмяОсновнойГруппы+"Право";
	
	ГруппаФормы = Форма.Элементы.Добавить(ИмяОсновнойГруппы, Тип("ГруппаФормы"), Родитель);
	
	Если Не ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		ПараметрыОтображения = Новый Структура;
	КонецЕсли;
	
	#Область ЗначенияПоУмолчанию
	Если Не ПараметрыОтображения.Свойство("Вид") Тогда
		ПараметрыОтображения.Вставить("Вид", ВидГруппыФормы.ОбычнаяГруппа);
	КонецЕсли;
	Если Не ПараметрыОтображения.Свойство("Группировка") Тогда
		ПараметрыОтображения.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	КонецЕсли;
	Если Не ПараметрыОтображения.Свойство("ОтображатьЗаголовок") Тогда
		ПараметрыОтображения.Вставить("ОтображатьЗаголовок", Ложь);
	КонецЕсли;
	#КонецОбласти
	
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ГруппаФормы,ПараметрыОтображения);
	КонецЕсли;

	РасположитьПередЭлементом(КонтекстЭлемента, ГруппаФормы);
		
	ПараметрыОтображенияВложенных = Новый Структура;
	ПараметрыОтображенияВложенных.Вставить("Вид", ПараметрыОтображения.Вид);
	ПараметрыОтображенияВложенных.Вставить("Группировка", ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	ПараметрыОтображенияВложенных.Вставить("ОтображатьЗаголовок", ПараметрыОтображения.ОтображатьЗаголовок);
	
	КонтекстНовогоЭлемента = СоздатьКонтекстЭлемента(Форма, ГруппаФормы);
	
	ДобавитьГруппуНаФорму(КонтекстНовогоЭлемента, ИмяГруппыЛево, ПараметрыОтображенияВложенных);
	ДобавитьГруппуНаФорму(КонтекстНовогоЭлемента, ИмяГруппыПраво, ПараметрыОтображенияВложенных);

	Возврат ГруппаФормы;

КонецФункции

Функция ДобыавитьГруппуОбычную(Форма, ИмяЭлемента, Родитель = Неопределено, РасположитьПередЭлементом = Неопределено, Группировка = Неопределено, ПараметрыОтображения = Неопределено) Экспорт
	
	Если РасположитьПередЭлементом = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(ИмяЭлемента, Тип("ГруппаФормы"), Родитель);
	Иначе
		НовыйЭлемент = Форма.Элементы.Вставить(ИмяЭлемента, Тип("ГруппаФормы"), Родитель, РасположитьПередЭлементом);
	КонецЕсли;
	
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	
	Если ЗначениеЗаполнено(Группировка) Тогда
		НовыйЭлемент.Группировка = Группировка;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ПараметрыОтображения);
	КонецЕсли;

	Возврат НовыйЭлемент;
	
КонецФункции

#КонецОбласти

#Область ДобавлениеТаблиц

// Функция добавляет таблицу на форму
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяПоля - Строка - Имя создааваемой таблицы.
//	ПутьКДанным - Строка - Имя создааваемого поля.
//	СтруктураКолонок - Структура - структура, по данным которой будут созданы колонки 
//		у которых значение элемента структуры будет соответствовать реквизиту таблицы, 
//		а ключ будет именем добавляемого поля.
//	Родитель - Произвольный - Ссылка на родителя.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//
//Пример:
//
//	СтруктураКолонок = Новый Структура;
//	СтруктураКолонок.Вставить("МенеджерыМенеджер", "Менеджер");
//	СтруктураКолонок.Вставить("МенеджерыНаправлениеДеятельности", "НаправлениеДеятельности");
//	ккПодготовкаФорм.ДобавитьТаблицуНаФорму(ЭтаФорма, "ТаблицаМенеджеры", "Объект.ккМенеджеры", СтруктураКолонок, Элементы.ГруппаРодительБизнесРегион);
//
Функция ДобавитьТаблицуНаФорму(КонтекстЭлемента, ИмяПоля, ПутьКДанным, СтруктураКолонок = Неопределено, ПараметрыОтображения = Неопределено) Экспорт
	
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
	
	ПолеФормы = Форма.Элементы.Добавить(ИмяПоля, Тип("ТаблицаФормы"), Родитель);
	
	Если ПараметрыОтображения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПолеФормы, ПараметрыОтображения);
	КонецЕсли;
	
	ПолеФормы.ПутьКДанным = ПутьКДанным;
	
	РасположитьПередЭлементом(КонтекстЭлемента, ПолеФормы);
	
	Если СтруктураКолонок <> Неопределено Тогда
		Для Каждого Элемент Из СтруктураКолонок Цикл
			
			КонтекстНовогоЭлемента = СоздатьКонтекстЭлемента(Форма, ПолеФормы);
				
			Параметры = Новый Структура;
			Параметры.Вставить("ПутьКДанным", ПутьКДанным+"."+Элемент.Значение);
			ДобавитьПолеНаФорму(КонтекстНовогоЭлемента, Элемент.Ключ, Параметры);
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПолеФормы;
	
КонецФункции

// Функция добавляет поле таблицы на форму
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ПутьТабличнойЧасти - Строка - Путь к табличной части, колонку которая выводится.
//	ИмяКолонкиТабличнойЧасти - Строка - Имя колонки, которая выводится.
//	Родитель - Произвольный - Ссылка на родителя.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства поля.
//	ЭтоПолеВвода - Булево - Определяет возможность ввода в добавляемое поле
//
// Возвращаемое значение:
//	ЭлементФормы - добавленный на форму, новый элемент формы.
//
//Пример:
//
// ккПодготовкаФорм.ДобавитьПолеТабличнойЧастиНаформу(ЭтаФорма, "Объект.Товары", "ккДатаОтгрузки", Элементы.Товары);
//
Функция ДобавитьПолеТабличнойЧастиНаформу(КонтекстЭлемента, 
											ПутьТабличнойЧасти, 
											ИмяКолонкиТабличнойЧасти, 
											Знач ПараметрыОтображения = Неопределено) Экспорт

	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
	
	ПутьКДанным = СокрЛП(ПутьТабличнойЧасти)+"."+СокрЛП(ИмяКолонкиТабличнойЧасти);
	
	МассивПутьКТабличнойЧасти = РазложитьСтрокуВМассивПодстрок(ПутьТабличнойЧасти, ".", Истина);
	ПоследнееСлово = МассивПутьКТабличнойЧасти[МассивПутьКТабличнойЧасти.Количество()-1];
	ИмяПоляТабличнойЧасти = СокрЛП(ПоследнееСлово)+СокрЛП(ИмяКолонкиТабличнойЧасти);
	
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		Если Не ПараметрыОтображения.Свойство("ПутьКДанным") Тогда
			ПараметрыОтображения.Вставить("ПутьКДанным", ПутьКДанным);
		КонецЕсли;
	Иначе
		ПараметрыОтображения = Новый Структура;
		ПараметрыОтображения.Вставить("ПутьКДанным", ПутьКДанным);
	КонецЕсли;
	
	КонтекстНовогоЭлемента = СоздатьКонтекстЭлемента(Форма, Родитель);
	
	ПолеФормы = ДобавитьПолеНаФорму(КонтекстНовогоЭлемента, ИмяПоляТабличнойЧасти, ПараметрыОтображения);
	
	РасположитьПередЭлементом(КонтекстЭлемента, ПолеФормы);

	Возврат ПолеФормы;
	
КонецФункции

#КонецОбласти

#Область ДобавлениеКоманд

// Функция добавляет команду на форму
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяКнопки - Строка - Имя создааваемой кнопки.
//	ВидКнопки - ВидКнопкиФормы - Вид кнопки.
//	ИмяКоманды - Строка - Имя команды.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//	Родитель - Произвольный - Ссылка на родителя.
//
Функция ДобавитьКнопкуНаФорму(КонтекстЭлемента, ИмяКнопки, ВидКнопки, ИмяКоманды, ПараметрыОтображения = Неопределено) Экспорт

	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
		 
	Кнопка = Форма.Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), Родитель);
	Кнопка.Вид = ВидКнопки;
	Кнопка.ИмяКоманды = ИмяКоманды;

	РасположитьПередЭлементом(КонтекстЭлемента, Кнопка);

	Если ПараметрыОтображения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Кнопка, ПараметрыОтображения);
	КонецЕсли;
	
	Возврат Кнопка;
	
КонецФункции

// Функция добавляет команду и гиперссылку на форму
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяКоманды - Строка - Имя создааваемой команды.
//	ПроцедураОбработки - Строка - Имя процедуры обработки.
//	ЗаголовокГиперссылки - Строка - Заголовок команды.
//	ГруппаВывода - Произвольный - Ссылка на родителя.
//
Функция ДобавитьКомандуИГиперссылкуНаФорму(КонтекстЭлемента, ИмяКоманды, ПроцедураОбработки, ЗаголовокГиперссылки, ГруппаВывода) Экспорт
	
	ДобавитьКомандуНаФорму(КонтекстЭлемента,
						ИмяКоманды,
						ПроцедураОбработки,
						ЗаголовокГиперссылки);
	
	Кнопка = ДобавитьКнопкуНаФорму(КонтекстЭлемента,
						ИмяКоманды,
						ВидКнопкиФормы.КнопкаКоманднойПанели,
						ИмяКоманды,
						Новый Структура("Вид", ВидКнопкиФормы.Гиперссылка));
						
	Возврат Кнопка;
	
КонецФункции

// Функция добавляет команду и кнопку командной панели
//  на форму будет добавлена команда с действием "Подключаемый_"+ИмяКоманды
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяКоманды - Строка - Имя создааваемой команды.
//	ЗаголовокКнопки - Строка - Заголовок команды.
//	ГруппаВывода - Произвольный - Ссылка на родителя.
//	РасположитьПередЭЛементом - ГруппаФормы; ТаблицаФормы; УправляемаяФорма - Элемент перед которым требуется разместить новый элемент.
//
Функция ДобавитьКомандуИКнопкуКоманднойПанели(КонтекстЭлемента, ИмяКоманды, ЗаголовокКнопки, ГруппаВывода, РасположитьПередЭлементом = Неопределено) Экспорт
	
	ИмяДействия = "Подключаемый_" + ИмяКоманды;
	
	ДобавитьКомандуНаФорму(КонтекстЭлемента,
						ИмяКоманды, 
						ИмяДействия,
						ЗаголовокКнопки);
	
	Кнопка = ДобавитьКнопкуНаФорму(КонтекстЭлемента,
						ИмяКоманды,
						ВидКнопкиФормы.КнопкаКоманднойПанели,
						ИмяКоманды);
	
	Возврат Кнопка;
	
КонецФункции

// Функция добавляет команду и кнопку формы
//  на форму будет добавлена команда с действием "Подключаемый_"+ИмяКоманды
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяКоманды - Строка - Имя создааваемой команды.
//	ЗаголовокКнопки - Строка - Заголовок команды.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//	Родитель - Произвольный - Ссылка на родителя.
//	РасположитьПередЭЛементом - ГруппаФормы; ТаблицаФормы; УправляемаяФорма - Элемент перед которым требуется разместить новый элемент.
//
Функция ДобавитьКомандуИКнопкуНаФорму(КонтекстЭлемента, ИмяКоманды, ЗаголовокКнопки, ПараметрыОтображения = Неопределено, Родитель = Неопределено, РасположитьПередЭлементом = Неопределено) Экспорт
	
	ИмяДействия = "Подключаемый_" + ИмяКоманды;
	
	ДобавитьКомандуНаФорму(КонтекстЭлемента,
						ИмяКоманды, 
						ИмяДействия,
						ЗаголовокКнопки);
	
	Кнопка = ДобавитьКнопкуНаФорму(КонтекстЭлемента,
						ИмяКоманды,
						ВидКнопкиФормы.ОбычнаяКнопка,
						ИмяКоманды,
						ПараметрыОтображения);
	
	Возврат Кнопка;
	
КонецФункции

// Функция добавляет команду на форму
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяКоманды - Строка - Имя создааваемой команды.
//	ИмяДействия - Строка - Действие команды.
//	Заголовок - Строка - Заголовок команды.
//	Подсказка - Строка - Подсказка команды.
//
Функция ДобавитьКомандуНаФорму(КонтекстЭлемента, ИмяКоманды, ИмяДействия, Заголовок, Подсказка = "") Экспорт

	Форма = КонтекстЭлемента.Форма;
	
	Команда = Форма.Команды.Добавить(ИмяКоманды);
	Команда.Действие = ИмяДействия;
	Команда.Заголовок = Заголовок;
	Команда.Подсказка = Подсказка;
	
	Возврат Команда;
	
КонецФункции

#КонецОбласти

#Область ДобавлениеРеквизитовОбъекта

// Функция добавляет реквизит объекта определяя ВидПоляФормы по типу значения
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяРеквизита - Строка - Имя создааваемого поля и он же имя реквизита объекта.
//	Родитель - Произвольный - Ссылка на родителя.
//	ЭтоПолеВвода - Булево - Определяет возможность ввода в добавляемое поле
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//	РасположитьПередЭлементом - ВсеЭлементыФормы - Указывает, перед каким элеменом формы разместить добавляемую группу
//
//Пример:
//	ккПодготовкаФорм.ДобавитьРеквизитОбъектаНаФорму(ЭтаФорма, "ккВидДоговора", Элементы.ГруппаШапкаПраво);
//
Функция ДобавитьРеквизитОбъектаНаФорму(КонтекстЭлемента,
										ИмяРеквизита, 
										ПараметрыОтображения = Неопределено) Экспорт
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;	

	Если ТипЗнч(Форма.Объект[ИмяРеквизита]) = Тип("Булево") Тогда
											
		Если ПараметрыОтображения  = Неопределено Тогда
			ПараметрыОтображения = Новый Структура("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);
		ИначеЕсли Не ПараметрыОтображения.Свойство("ПоложениеЗаголовка") Тогда
			ПараметрыОтображения.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Право);
		КонецЕсли;
		
		Возврат ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента, ИмяРеквизита, ПараметрыОтображения);
	Иначе
		Возврат ДобавитьПолеНаФормуРеквизитОбъекта(КонтекстЭлемента, ИмяРеквизита, ПараметрыОтображения);
	КонецЕсли;

КонецФункции

// Функция добавляет поле реквизита объекта на форму и возвращает добавленный элемент
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяРеквизита - Строка - Имя создааваемого поля и он же имя реквизита объекта.
//	Родитель - Произвольный - Ссылка на родителя.
//	ЭтоПолеВвода - Булево - Определяет возможность ввода в добавляемое поле
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//	РасположитьПередЭлементом - ВсеЭлементыФормы - Указывает, перед каким элеменом формы разместить добавляемую группу
//
//Пример:
//	ккПодготовкаФорм.ДобавитьПолеНаФормуРеквизитОбъекта(ЭтаФорма, "ккВидДоговора", Элементы.ГруппаШапкаПраво, Истина);
//
Функция ДобавитьПолеНаФормуРеквизитОбъекта(КонтекстЭлемента,
											ИмяРеквизита, 
											ПараметрыОтображения = Неопределено) Экспорт
											
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;												
	
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		Если Не ПараметрыОтображения.Свойство("ПутьКДанным") Тогда
			ПараметрыОтображения.Вставить("ПутьКДанным", "Объект."+ИмяРеквизита);
		КонецЕсли;
	Иначе
		ПараметрыОтображения = Новый Структура;
		ПараметрыОтображения.Вставить("ПутьКДанным", "Объект."+ИмяРеквизита);
	КонецЕсли;
	
	ПолеФормы = Форма.Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Родитель);
	ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
	
	ЗаполнитьЗначенияСвойств(ПолеФормы, ПараметрыОтображения);
	
	РасположитьПередЭлементом(КонтекстЭлемента, ПолеФормы);
	
	ПараметрыОтображения.Удалить("ПутьКДанным");
	
	Возврат ПолеФормы;
	
КонецФункции

// Функция добавляет поле реквизита объекта на форму с заданным значением МаксимальнаяШирина = 28
//	для вывода в шапку формы, согласно рекомендации адаптации интерфейсов
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяРеквизита - Строка - Имя создааваемого поля и он же имя реквизита объекта.
//	Родитель - Произвольный - Ссылка на родителя.
//	ЭтоПолеВвода - Булево - Определяет возможность ввода в добавляемое поле
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//
//Пример:
//	ккПодготовкаФорм.ДобавитьПолеВШапкуФормыРеквизитОбъекта(ЭтаФорма, "ккВидДоговора", Элементы.ГруппаШапкаПраво, Истина);
//
Функция ДобавитьПолеВШапкуФормыРеквизитОбъекта(КонтекстЭлемента, ИмяРеквизита, ПараметрыОтображения = Неопределено) Экспорт
											
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		Если Не ПараметрыОтображения.Свойство("АвтоМаксимальнаяШирина") Тогда
			ПараметрыОтображения.Вставить("АвтоМаксимальнаяШирина", Ложь);
			ПараметрыОтображения.Вставить("МаксимальнаяШирина", 28);
		КонецЕсли;
	Иначе
		ПараметрыОтображения = Новый Структура;
		ПараметрыОтображения.Вставить("МаксимальнаяШирина", 28);
		ПараметрыОтображения.Вставить("АвтоМаксимальнаяШирина", Ложь);
	КонецЕсли;
	
	ПолеФормы = ДобавитьПолеНаФормуРеквизитОбъекта(КонтекстЭлемента, ИмяРеквизита, ПараметрыОтображения);
	
	Возврат ПолеФормы;
	
КонецФункции

// Добавляет многострочное поле реквизита объекта и возвращает элемент формы, добавленный на форму.
//
// Параметры:
//	Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяРеквизита - Строка - Имя создааваемого поля и он же имя реквизита объекта.
//	Родитель - ГруппаФормы; ТаблицаФормы; УправляемаяФорма - Родитель элемента.
//	РасположитьПередЭЛементом - ГруппаФормы; ТаблицаФормы; УправляемаяФорма - Элемент перед которым требуется разместить новый элемент.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//	Высота - Число - Высота многострочного поля.
//
// Возвращаемое значение:
//	ЭлементФормы - добавленный на форму, новый элемент формы.
//
Функция ДобавитьМногострочноеПолеРеквизитОбъекта(КонтекстЭлемента,
												ИмяРеквизита,
												ПараметрыОтображения = Неопределено,
												Высота = 2) Экспорт
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
		
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		Если Не ПараметрыОтображения.Свойство("ПутьКДанным") Тогда
			ПараметрыОтображения.Вставить("ПутьКДанным", "Объект."+ИмяРеквизита);
		КонецЕсли;
	Иначе
		ПараметрыОтображения = Новый Структура;
		ПараметрыОтображения.Вставить("ПутьКДанным", "Объект."+ИмяРеквизита);
	КонецЕсли;
	
	ПолеФормы = Форма.Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Родитель);
	ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
	
	ЗаполнитьЗначенияСвойств(ПолеФормы, ПараметрыОтображения);
	
	РасположитьПередЭлементом(КонтекстЭлемента, ПолеФормы);
	
	ПолеФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	ПолеФормы.РастягиватьПоВертикали = Ложь;
	ПолеФормы.АвтоМаксимальнаяВысота = Ложь;
	ПолеФормы.АвтоМаксимальнаяШирина = Ложь;
	ПолеФормы.МногострочныйРежим = Истина;
	ПолеФормы.Высота = Высота;

	Возврат ПолеФормы;
	
КонецФункции

// Функция добавляет флажок реквизита объекта на форму и возвращает добавленный элемент
//
// Параметры:
//  Форма - УправляемаяФорма - форма для которой выполняется действие.
//	ИмяРеквизита - Строка - Имя создааваемого поля и он же имя реквизита объекта.
//	Родитель - Произвольный - Ссылка на родителя.
//	ПараметрыОтображения - Структура - Структура по которой заполняются свойства команды.
//	РасположитьПередЭЛементом - ГруппаФормы; ТаблицаФормы; УправляемаяФорма - Элемент перед которым требуется разместить новый элемент.
//
//Пример:
//	ккПодготовкаФорм.ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(ЭтаФорма, "ккВидДоговора", Элементы.ГруппаШапкаПраво);
//
Функция ДобавитьПолеФлажкаНаФормуРеквизитОбъекта(КонтекстЭлемента,
											ИмяРеквизита, 
											Родитель = Неопределено,
											ПараметрыОтображения = Неопределено,
											РасположитьПередЭлементом = Неопределено) Экспорт
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
		
	Если ТипЗнч(ПараметрыОтображения) = Тип("Структура") Тогда
		Если Не ПараметрыОтображения.Свойство("ПутьКДанным") Тогда
			ПараметрыОтображения.Вставить("ПутьКДанным", "Объект."+ИмяРеквизита);
		КонецЕсли;
	Иначе
		ПараметрыОтображения = Новый Структура;
		ПараметрыОтображения.Вставить("ПутьКДанным", "Объект."+ИмяРеквизита);
	КонецЕсли;
	
	ПолеФормы = Форма.Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Родитель);
	ПолеФормы.Вид = ВидПоляФормы.ПолеФлажка;
	
	ЗаполнитьЗначенияСвойств(ПолеФормы, ПараметрыОтображения);
	
	РасположитьПередЭлементом(КонтекстЭлемента, ПолеФормы);

	ПараметрыОтображения.Удалить("ПутьКДанным");
	
	Возврат ПолеФормы;
	
КонецФункции

// Функция группу кнопок на форму.
//
// Параметры:
//	Форма - УправляемаяФорма - форма для которой выполняется действие.
//	Родитель - КоманднаяПанель - Родитель элемента.
//	РасположитьПередЭлементом - ГруппаФормы; ТаблицаФормы; УправляемаяФорма - Элемент перед которым требуется разместить новый элемент.
//
// Возвращаемое значение:
//	ЭлементФормы - добавленная на форму, группа кнопок формы.
//
Функция ДобавитьГруппуКнопок(КонтекстЭлемента) Экспорт 
		
	Форма = КонтекстЭлемента.Форма;
	Родитель = КонтекстЭлемента.Родитель;
	
	Сч = 0;
	ЭлементНайден = Истина;
	Пока ЭлементНайден = Истина Цикл
		
		Шаблон = НСтр("ru = 'ГруппаКнопокФормы%1'");
		ИмяРеквизита = СтрШаблон(Шаблон, Сч);
		
		Если Форма.Элементы.Найти(ИмяРеквизита) = Неопределено Тогда
			
			ЭлементНайден = Ложь;
			ГруппаКонпокФормы = Форма.Элементы.Добавить(ИмяРеквизита, Тип("ГруппаФормы"), Родитель);
			ГруппаКонпокФормы.Вид = ВидГруппыФормы.ГруппаКнопок;
			
			РасположитьПередЭлементом(КонтекстЭлемента, ГруппаКонпокФормы);
					
			Возврат ГруппаКонпокФормы;
		КонецЕсли;
		Сч = Сч + 1;
	КонецЦикла;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПереместитьЭлемент(ПолеФормы, РасположитьПередЭлементом, Родитель, Форма)

	Если РасположитьПередЭлементом <> Неопределено Тогда
		Форма.Элементы.Переместить(ПолеФормы, Родитель, РасположитьПередЭЛементом);
	КонецЕсли;

КонецПроцедуры

Функция РазложитьСтрокуВМассивПодстрок(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь) Экспорт
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	Позиция = СтрНайти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		Позиция = СтрНайти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

Процедура РасположитьПередЭлементом(КонтекстЭлемента, Элемент)

	Форма = КонтекстЭлемента.Форма;
	Родитель = Неопределено;
	
	РасположитьПередЭлементом = Неопределено;
	КонтекстЭлемента.Свойство("Родитель", Родитель);
	КонтекстЭлемента.Свойство("РасположитьПередЭлементом", РасположитьПередЭлементом);

	Если РасположитьПередЭлементом <> Неопределено Тогда
		ПереместитьЭлемент(Элемент, РасположитьПередЭлементом, Родитель, Форма);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти
